// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    //// Replace this comment with your code.
    Mux16(a= x, b= false, sel= zx, out= tempx);
    Mux16(a= y, b= false, sel= zy, out= tempy);

    Not16(in= tempx, out= notx);
    Not16(in= tempy, out= noty);
    Mux16(a= tempx, b= notx, sel= nx, out= tempx2);
    Mux16(a= tempy, b= noty, sel= ny, out= tempy2);

    Add16(a= tempx2, b= tempy2, out= xAddy);
    And16(a= tempx2, b= tempy2, out= xAndy);

    Mux16(a= xAndy, b= xAddy, sel= f, out= tempout);

    Not16(in= tempout, out= tempout2);
    Mux16(a= tempout, b= tempout2, sel= no, out= out, out[0..7]=out07, out[8..15]=out815, out[15]=out15);

    Or8Way(in= out07, out= tempor);
    Or8Way(in= out815, out= tempor2);
    Or(a= tempor, b= tempor2, out= notzr);
    Not(in= notzr, out= zr);

    And(a= out15, b= true, out= ng);
}